table 51300 "Generation Buffer"
{
    DataClassification = CustomerContent;
    TableType = Temporary;

    fields
    {
        field(1; "Generation ID"; Integer)
        {
            DataClassification = CustomerContent;
            Caption = 'Generation ID';
            ToolTip = 'Specifies the unique identifier for this generation.';
        }
        field(2; "Input Text"; Blob)
        {
            DataClassification = CustomerContent;
            Caption = 'Input Text';
            ToolTip = 'Stores the input text used for generation as a blob to handle large text.';
        }
        field(3; "Response Text"; Blob)
        {
            DataClassification = CustomerContent;
            Caption = 'Response Text';
            ToolTip = 'Stores the response text generated by the AI model as a blob to handle large text.';
        }
        field(5; ResponseGenerated; Boolean)
        {
            DataClassification = CustomerContent;
            Caption = 'Response Generated';
            ToolTip = 'Indicates whether a response has been generated for this input.';
        }
    }

    keys
    {
        key(PK; "Generation ID")
        {
            Clustered = true;
        }
    }

    procedure SetInputText(InputText: Text)
    var
        OutStream: OutStream;
    begin
        "Input Text".CreateOutStream(OutStream, TextEncoding::UTF8);
        OutStream.WriteText(InputText);
    end;

    procedure GetInputText() Result: Text
    var
        TypeHelper: Codeunit "Type Helper";
        InStream: InStream;
    begin
        CalcFields("Input Text");
        "Input Text".CreateInStream(InStream, TextEncoding::UTF8);
        Result := TypeHelper.ReadAsTextWithSeparator(InStream, TypeHelper.LFSeparator());
    end;

    procedure SetResponseText(ResponseText: Text)
    var
        OutStream: OutStream;
    begin
        "Response Text".CreateOutStream(OutStream, TextEncoding::UTF8);
        OutStream.WriteText(ResponseText);
    end;

    procedure GetResponseText() Result: Text
    var
        TypeHelper: Codeunit "Type Helper";
        InStream: InStream;
    begin
        CalcFields("Response Text");
        "Response Text".CreateInStream(InStream, TextEncoding::UTF8);
        Result := TypeHelper.ReadAsTextWithSeparator(InStream, TypeHelper.LFSeparator());
    end;
}
